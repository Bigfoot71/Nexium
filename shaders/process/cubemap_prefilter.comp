/* cubemap_prefilter.comp -- Prefiltered cubemap generation compute shader
 *
 * Copyright (c) 2025 Le Juez Victor
 *
 * This software is provided 'as-is', without any express or implied warranty.
 * For conditions of distribution and use, see accompanying LICENSE file.
 */

/* === Profile Specific === */

#ifdef GL_ES
precision highp float;
#endif

/* === Includes === */

#include "../include/math.glsl"

/* === Constants === */

const float EPSILON = 1e-7;
const uint SAMPLE_COUNT = 64u;

/* === Local Size === */

layout(local_size_x = 8, local_size_y = 8, local_size_z = 6) in;

/* === Uniforms === */

layout(binding = 0) uniform samplerCube uTexCubemap;
layout(binding = 1, rgba16f) uniform writeonly highp imageCubeArray uTargetCubemap;

/* === Samplers === */

layout(location = 0) uniform int uTargetIndex;          // Destination cubemap index
layout(location = 1) uniform float uRoughness;          // Roughness level for this mip level

/* === Helper functions === */

vec3 GetDirection(ivec3 globalId, float size)
{
    vec2 uvc = (vec2(globalId.xy) + 0.5) / size;
    vec2 uv = fma(uvc, vec2(2.0), vec2(-1.0));
    switch(globalId.z) {
    case 0: return normalize(vec3( 1.0, -uv.y, -uv.x)); // +X
    case 1: return normalize(vec3(-1.0, -uv.y,  uv.x)); // -X
    case 2: return normalize(vec3( uv.x,  1.0,  uv.y)); // +Y
    case 3: return normalize(vec3( uv.x, -1.0, -uv.y)); // -Y
    case 4: return normalize(vec3( uv.x, -uv.y,  1.0)); // +Z
    case 5: return normalize(vec3(-uv.x, -uv.y, -1.0)); // -Z
    }
    return vec3(0.0);
}

float DistributionGGX(vec3 N, vec3 H, float a2)
{
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;

    float nom = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = M_PI * denom * denom;

    return nom / max(denom, EPSILON);
}

float RadicalInverse_VdC(uint bits)
{
    // Efficient VanDerCorpus calculation
    // See: http://holger.dammertz.org/stuff/notes_HammersleyOnHemisphere.html
    bits = (bits << 16u) | (bits >> 16u);
    bits = ((bits & 0x55555555u) << 1u) | ((bits & 0xAAAAAAAAu) >> 1u);
    bits = ((bits & 0x33333333u) << 2u) | ((bits & 0xCCCCCCCCu) >> 2u);
    bits = ((bits & 0x0F0F0F0Fu) << 4u) | ((bits & 0xF0F0F0F0u) >> 4u);
    bits = ((bits & 0x00FF00FFu) << 8u) | ((bits & 0xFF00FF00u) >> 8u);
    return float(bits) * 2.3283064365386963e-10; // / 0x100000000
}

vec2 Hammersley(uint i, uint N)
{
    return vec2(float(i) / float(N), RadicalInverse_VdC(i));
}

vec3 ImportanceSampleGGX(vec2 Xi, float a, mat3 OBN)
{
    float phi = M_TAU * Xi.x;
    float cosTheta = sqrt((1.0 - Xi.y) / (1.0 + (a * a - 1.0) * Xi.y));
    float sinTheta = sqrt(max(1.0 - cosTheta * cosTheta, 0.0));

    // Spherical coordinates -> Cartesian (halfway vector)
    vec3 H;
    H.x = cos(phi) * sinTheta;
    H.y = sin(phi) * sinTheta;
    H.z = cosTheta;

    // Transformation to world space
    return normalize(OBN * H);
}

float ComputeMipLevel(float pdf, float cubeResolution)
{
    float saTexel = 4.0 * M_PI / (6.0 * cubeResolution * cubeResolution);
    float saSample = 1.0 / (float(SAMPLE_COUNT) * max(pdf, EPSILON));
    return max(0.0, 0.5 * log2(saSample / saTexel));
}

/* === Program === */

void main()
{
    /* --- Check if we're within bounds --- */

    float mipImageSize = float(imageSize(uTargetCubemap).x);
    ivec3 globalId = ivec3(gl_GlobalInvocationID.xyz);

    if (globalId.x >= int(mipImageSize) || globalId.y >= int(mipImageSize)) {
        return;
    }

    /* --- Calculate cubemap/face index in the texture array --- */

    int layer = uTargetIndex * 6 + globalId.z;
    
    /* --- Get the world space direction for this texel --- */

    vec3 N = GetDirection(globalId, mipImageSize);
    vec3 R = N;
    vec3 V = R;

    /* --- Handle the case where roughness = 0 (perfect mirror) --- */

    if (uRoughness < EPSILON) {
        vec4 color = textureLod(uTexCubemap, N, 0.0);
        imageStore(uTargetCubemap, ivec3(globalId.xy, layer), color);
        return;
    }

    /* --- Pre calculate invariants --- */

    float a = uRoughness * uRoughness;
    float a2 = a * a;

    mat3 OBN = M_OrthonormalBasis(N);

    /* --- Convolve environment map --- */

    vec3 prefilteredColor = vec3(0.0);
    float totalWeight = 0.0;

    for (uint i = 0u; i < SAMPLE_COUNT; ++i)
    {
        vec2 Xi = Hammersley(i, SAMPLE_COUNT);
        vec3 H = ImportanceSampleGGX(Xi, a, OBN);
        vec3 L = normalize(reflect(-V, H));

        float NdotL = max(dot(N, L), 0.0);

        if (NdotL > EPSILON)
        {
            float D = DistributionGGX(N, H, a2);
            float NdotH = max(dot(N, H), 0.0);
            float HdotV = max(dot(H, V), 0.0);

            float pdf = (D * NdotH) / max(4.0 * HdotV, EPSILON);
            float mipLevel = ComputeMipLevel(pdf, float(textureSize(uTexCubemap, 0).x));
            mipLevel = clamp(mipLevel, 0.0, float(textureQueryLevels(uTexCubemap) - 1));

            vec3 sampleColor = textureLod(uTexCubemap, L, mipLevel).rgb;
            prefilteredColor += sampleColor * NdotL;
            totalWeight += NdotL;
        }
    }

    /* --- Normalize and store --- */

    prefilteredColor = prefilteredColor / max(totalWeight, EPSILON);
    imageStore(uTargetCubemap, ivec3(globalId.xy, layer), vec4(prefilteredColor, 1.0));
}
